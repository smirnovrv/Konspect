#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТабИзмСтрок = СформироватьТаблицуЗаметок();
	Если Не ТабИзмСтрок = Неопределено Тогда
		Объект.ОсновнойТекст 	= ОсновнойТекстHTMLСервер.ПолучитьОсновнойТекстИзТаблицы(ТабИзмСтрок);
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ЗаполнятьАвтоматическиПриИзменении(Элемент)
	ЗаполнятьАвтоматическиПриИзмененииНаСервере();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Дозаполнить(Команда)
	ДозаполнитьНаСервере();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ЗаполнятьАвтоматическиПриИзмененииНаСервере()
	Если Объект.ЗаполнятьАвтоматически Тогда
		Объект.Заметки.Очистить();				
		Объект.Заметки.Загрузить(ПолучитьСписокЗаметок());		
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ДозаполнитьНаСервере()
	
	ТаблицаЗаметок = ПолучитьСписокЗаметок();
	
	Для Каждого СтрокаТаб Из ТаблицаЗаметок Цикл
		Если Объект.Заметки.НайтиСтроки(Новый Структура("Заметка", СтрокаТаб.Заметка)).Количество() = 0 Тогда
			НоваяСтрока = Объект.Заметки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаб);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокЗаметок()
	
	Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Заметки.Ссылка КАК Заметка
			|ИЗ
			|	Справочник.Заметки КАК Заметки
			|ГДЕ
			|	Заметки.Тема = &Тема";
		
		Запрос.УстановитьПараметр("Тема", Объект.Ссылка);
		
		ТаблицаЗаметок = Запрос.Выполнить().Выгрузить();
		
	Возврат ТаблицаЗаметок;
	
КонецФункции

&НаСервере
Функция СформироватьТаблицуЗаметок()
	
	ТабИзмСтрок = Неопределено;

	Для Каждого стрТЧ Из Объект.Заметки Цикл
		Если ТабИзмСтрок = Неопределено Тогда
			ТабИзмСтрок = стрТЧ.Заметка.Состав.Выгрузить();
		Иначе
			Для Каждого СтрокаСостав Из стрТЧ.Заметка.Состав Цикл
				Если СтрокаСостав.НомерСтроки > 4 И СтрокаСостав.НомерСтроки < стрТЧ.Заметка.Состав.Количество()-1 Тогда
					НоваяСтрока = ТабИзмСтрок.Вставить(ТабИзмСтрок.Количество()-2);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСостав);
				КонецЕсли;	 					
			КонецЦикла;	
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТабИзмСтрок;
	
КонецФункции
	
#КонецОбласти